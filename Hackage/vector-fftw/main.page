---
format: markdown+lhs
title: control-monad-loop
...

This is a DFT solver that conforms to
[DFT contest](../what-is-the-fastest-dft-in-haskell/main)
regulation. Our module:

> {-# OPTIONS -Wall #-}
> module ClientVectorFFTW where

uses
[vector-fftw](http://hackage.haskell.org/packages/vector-fftw)
package:

> import Control.Applicative
> import qualified Numeric.FFT.Vector.Invertible as FFT
> import qualified Data.Vector.Unboxed as V
> import Data.Complex

and conforms to the [Solver](../what-is-the-fastest-dft-in-haskell/Solver.hs)
regulations.

> import Solver
> solver :: Solver
> solver prob = return (ans1, ans2)
>   where
>     n :: Int
>     n = probSize prob
>     realN :: Double
>     realN = fromIntegral n

The initial field, is a discrete hat function in the real part, and quadratic in the imaginary part.

>     initVect, finalVect :: V.Vector (Complex Double)
>     initVect = V.generate n initF
>     initF :: Int -> Complex Double
>     initF i = re :+ im
>       where
>         r = (fromIntegral i + 0.5) / realN
>         re | (x1 prob < r && r < x2 prob) = 1
>            | otherwise                    = 0
>         im = (r-y1 prob)*(r-y2 prob)

In vector-fftw, you declare the plan, then construct the map over array, as follows; 

>     planFwd = FFT.plan FFT.dft  n
>     planBwd = FFT.plan FFT.idft n
>     step :: V.Vector (Complex Double) -> V.Vector (Complex Double)
>     step = FFT.execute planBwd . FFT.execute planFwd

Regulation tells us to perfrom the set of forward then backward DFT, for
`(probRepeat prob)` times.

>     finalVect = foldl1 (.) (replicate (probRepeat prob) step) $ initVect

Then finally, contestants are to calculate the amplitudes both in the real space and the wave space, and return them.

>     amp ::  V.Vector (Complex Double) -> Double
>     amp =  V.sum . V.map ((^2) . magnitude)
>     ans1 = amp finalVect / realN
>     ans2 = amp (FFT.execute planFwd $ finalVect) / realN^2